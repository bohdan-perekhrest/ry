#compdef ry

# ry ZSH completion function
# Symlink (or copy) this file in your $fpath (like /usr/share/zsh/site-functions)
# and name it _ry: ln -s path/to/this/file _ry

local -a rubies
if (( CURRENT > 2 )); then
  shift words
  (( CURRENT-- ))
  local subcmd="$words[1]"

  case $subcmd in
    use|uninstall)
      # Get installed ruby versions
      local ry_rubies="${RY_RUBIES:-${XDG_DATA_HOME:-$HOME/.local/share}/ry/rubies}"
      if [[ -d "$ry_rubies" ]]; then
        rubies=(${ry_rubies}/*(:t))
      fi
    ;;
    install)
      if (( $+commands[ruby-build] )); then
        rubies=($(_call_program rubies ruby-build --definitions))
      fi
    ;;
    *)
      rubies=()
    ;;
  esac
  _describe rubies rubies
else
  local -a commands
  commands=(
    current:'show the current ruby version'
    use:'use the given ruby version'
    install:'install the given ruby version using ruby-build'
    uninstall:'remove the given ruby version'
    system:'switch to system ruby'
    setup:'output shell commands to add Ruby to PATH'
    help:'show help information'
    version:'show ry version'
  )

  # Also show installed rubies as options for direct selection
  local ry_rubies="${RY_RUBIES:-${XDG_DATA_HOME:-$HOME/.local/share}/ry/rubies}"
  if [[ -d "$ry_rubies" ]]; then
    rubies=(${ry_rubies}/*(:t))
  fi

  _describe commands commands
  _describe rubies rubies
fi
